rootProject.name = 'docs.nokee.dev'

void includeExternalBuild(String repositoryName, String repositoryUrl, String path = '') {
	// Opinionated on my local development layout
	if (file("../${repositoryName}").exists()) {
		includeBuild("../${repositoryName}/${path}")

	// Else, just clone the repository
	} else {
		if (!file(".repositories/${repositoryName}").exists()) {
			file('.repositories').mkdirs()
			"git clone ${repositoryUrl} .repositories/${repositoryName}".execute().waitFor()
		} else {
			"git pull".execute(null, file(".repositories/${repositoryName}")).waitFor()
		}
		includeBuild(".repositories/${repositoryName}/${path}")
	}
}

includeExternalBuild('toolbox', 'https://github.com/gradle-plugins/toolbox.git')
includeExternalBuild('gradle-native', 'https://github.com/nokeedev/gradle-native.git', 'gradle/plugins/docs-gradle-plugin') // Required because Gradle fails
includeExternalBuild('gradle-native', 'https://github.com/nokeedev/gradle-native.git', 'subprojects/templates') // Required because Gradle fails
includeExternalBuild('gradle-native', 'https://github.com/nokeedev/gradle-native.git')

include '0.1.0'

